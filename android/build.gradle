buildscript {
    ext.kotlin_version = '1.7.10'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        // START: FlutterFire Configuration
        classpath 'com.google.gms:google-services:4.3.14'
        classpath 'com.google.firebase:perf-plugin:1.4.1'
        // END: FlutterFire Configuration
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.7.1'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

configure([project(':rudder-sdk-android-core')]) {
    ext {
        VERSION_NAME = '1.8.1'
    }
}

allprojects {
    configurations.all {
        resolutionStrategy.dependencySubstitution.all { DependencySubstitution dependency ->
            if (dependency.requested instanceof ModuleComponentSelector && dependency.requested.group == 'com.rudderstack.android.sdk') {
                def targetProject = findProject(':rudder-sdk-android-core')
                if (targetProject != null) {
                    logger.info('bccm: Substituting rudder-sdk-android-core with local :rudder-sdk-android-core.')
                    dependency.useTarget targetProject
                    logger.info('bccm: Successfully substituted rudder-sdk-android-core with local :rudder-sdk-android-core.')
                } else {
                    logger.error('bccm: Couldnt find local project(:rudder-sdk-android-core).')
                }
            }
        }
    }
}
